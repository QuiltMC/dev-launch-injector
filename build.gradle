plugins {
    id 'java'
    id 'maven-publish'
    id 'checkstyle'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.quiltmc'
archivesBaseName = 'dev-launch-injector'
version = '1.0.1'

def ENV = System.getenv()
version = version

repositories {
	mavenCentral()
}

checkstyle {
	configFile = file("checkstyle.xml")
	toolVersion = '8.25'
}

// Target JDK 8
tasks.withType(JavaCompile).configureEach {
	if (JavaVersion.current().isJava9Compatible()) {
		options.release.set(8)
	} else {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
	}
}

java {
	// Publish sources and javadoc
	withSourcesJar()
	withJavadocJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_URL) {
			repositories.maven {
				url = ENV.MAVEN_URL

				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}